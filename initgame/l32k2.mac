.Z80

; BLOAD MODULE FOR 32KB ROMS PART 2 (FOR CASLINK2 PROJECT)
; COPYRIGHT (C) 1999-2008 ALEXEY PODREZOV

	ASEG
	ORG	9000h

START:	JP	START1

STARTA:	DW	00
ENDA:	DW	00
EXECA:	DW	00
CRC:	DB	00

CASLSTR:DB	"[CasLink2] Loading failed: CRC ERROR!",0

START1:	DI
	LD	HL,(STARTA)
      	LD	DE,(ENDA)
	EX	DE,HL
	SBC	HL,DE
	PUSH	HL
	POP	BC
	LD	HL,ROMCODE
        XOR	A
	PUSH	AF
START2:	POP	AF
	ADD	A,(HL)
	INC	HL
	DEC	BC
	PUSH	AF
	LD	A,C
	OR	A
	JP	NZ,START2
	LD	A,B
	OR	A
	JP	NZ,START2
	POP	AF
	LD	B,A
	LD	HL,CRC
	LD	A,(HL)
	CP	B
	JP	Z,START5

CRCERR:	EI
	CALL	006CH		; set screen 0
	LD	A,0FH
	LD	HL,0F3E9H
	LD	(HL),A
	LD	A,8
	INC	HL
	LD	(HL),A
	INC	HL
	LD	(HL),A
	CALL	0062H		; set color 15,8,8
	XOR	A
	CALL	00C3H		; clear screen
	CALL	00CFH		; unhide functional keys
	LD	HL,0101H
	CALL	00C6H		; set cursor position to 1:1
	LD	DE,CASLSTR
START3:	LD	A,(DE)
	OR	A
	JP	Z,START4
	INC	DE
	CALL	00A2H		; display character
	INC	H
	CALL	00C6H		; set next position
	JP	START3

START4:	LD	HL,0103H
	CALL	00C6H		; set cursor position to 1:3
	CALL	00C0H		; beep
	CALL	0156H		; clears keyboard buffer
	EI
	RET

START5:	LD	A,(0FFFFH)
	CPL
	PUSH	AF
	LD	C,A
	AND	0F0H
	LD	B,A
	LD	A,C
	RRCA
	RRCA
	RRCA
	RRCA
	AND	15
	OR	B
	LD	(0FFFFH),A
	IN	A,(0A8H)
	PUSH	AF
	AND	0F0H
	LD	B,A
	RRCA
	RRCA
	RRCA
	RRCA
	OR	B
	OUT	(0A8H),A
	LD	HL,(STARTA)
	LD	A,H
	AND	0F0H
	CP	80H
	JP	NZ,START6
	LD	HL,START6
      	LD	DE,ROMCODE+1
	EX	DE,HL
	SBC	HL,DE
	LD	B,H
	LD	C,L
	LD	HL,START6
	LD	DE,0F560H
	PUSH	DE
	LDIR
	RET
START6:	LD	HL,(STARTA)
      	LD	DE,(ENDA)
	EX	DE,HL
	SBC	HL,DE
	LD	B,H
	LD	C,L
	LD	HL,ROMCODE
	LD	DE,(STARTA)
	LDIR
	POP	AF
	OUT	(0A8H),A
	POP	AF
	LD	(0FFFFH),A
	RST	30H
	DW	0
	DW	0

ROMCODE:

END
